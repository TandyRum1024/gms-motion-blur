// 2019-07-29 01:16:41
#event create
#action action_inherited
#section Billboarded "NPC" sprites
time = 0;
z = size;

// random sprite for NPC stuff
randomize();
texOffset = global.textureList[@ ePTEX.NPC, 1];
npcIdx = choose(0, 1) * 2;
texIdx = texOffset + npcIdx;

// culling
size = 16;
radius = size; // culling radius

drawShadow = true;
#section Barrel throw counter
barrelCtr = room_speed * random_range(4, 6);
barrelInst = noone;

#event step Throw barrel
if (barrelCtr-- < 0)
{
    barrelCtr = room_speed * random_range(4, 6);
    if (barrelInst == noone || !instance_exists(barrelInst))
    {   
        var _dir = point_direction(x, y, oPlayer.x, oPlayer.y);
        var _barrel = instance_create(x, y, oBarrel3D);
        _barrel.vx = lengthdir_x(random_range(4, 8), _dir);
        _barrel.vy = lengthdir_y(random_range(4, 8), _dir);
        _barrel.vz = random_range(6, 9);
        barrelInst = _barrel;
    }
    else
    {
        repeat (irandom_range(3, 6))
        {
            particle3d_smoke_emit(16, 16 + irandom_range(-8, 8), barrelInst.x + irandom_range(-16, 16), barrelInst.y + irandom_range(-16, 16), barrelInst.z + irandom_range(-16, 16), random_range(-4, 4), random_range(-4, 4), random_range(0.1, 6));
        }
        
        instance_destroy(barrelInst);
        barrelInst = noone;
        barrelCtr = room_speed * random_range(1, 2);
    }
}
#section Update NPC animation
time += 0.1;
texIdx = texOffset + npcIdx + floor(time) % 2;

#event draw Keep this blank to prevent the GM to automatically draw stuff